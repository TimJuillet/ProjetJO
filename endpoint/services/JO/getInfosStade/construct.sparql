PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ex: <http://example.org/schema#>
PREFIX : <http://example.org/olympics#>

CONSTRUCT {
  ?venue rdf:type :Venue ;
         :name ?name ;
         :description ?desc ;
         :hasCoordinate ?coord ;
         :hasCapacity ?capacity .

  ?coord rdf:type :Coordinate ;
         :hasLatitude ?y ;
         :hasLongitude ?x .
}
WHERE {
  {
    SELECT ?name ?desc ?x ?y ?capacity
    WHERE {
      ?s ex:description ?desc ;
         ex:equip_x ?x ;
         ex:equip_y ?y ;
         ex:name ?name ;
         ex:nb_places ?capacity .
    }
    ORDER BY ?capacity
    LIMIT 1
  }
  
  # Générer les IRIs
  BIND(IRI(CONCAT("http://example.org/venue#", ENCODE_FOR_URI(?name), "_", ENCODE_FOR_URI(?desc))) AS ?venue)
  BIND(IRI(CONCAT("http://example.org/position#", ENCODE_FOR_URI(?name), "_Position")) AS ?coord)
}